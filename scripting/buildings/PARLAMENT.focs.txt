BuildingType
    name = "BLD_PARLAMENT"
    description = "BLD_PARLAMENT_DESC"
    captureresult = Destroy
    buildcost = 12 * [[BUILDING_COST_MULTIPLIER]] * (Statistic Count condition = And [Planet OwnedBy empire = Source.Owner])
    buildtime = 4
    location = And [
        Planet
        OwnedBy empire = Source.Owner
        Not Contains Building name = "BLD_PARLAMENT"
        TargetPopulation low = 1
        Number low = 0 high = 0 condition = And [
            Building name = "BLD_PARLAMENT"
            OwnedBy empire = Source.Owner
            ProducedByEmpire empire = Source.Owner
        ]
    ]
    enqueuelocation = And [
        // must own production location planet
        Planet
        OwnedBy empire = Source.Owner

        // can't build where another palace exists (even if not owned by this empire)
        Not Contains Building name = "BLD_PARLAMENT"

        // must have a non-trivial population
        TargetPopulation low = 1

        // can't enqueue if already own a self-built palace
        Number low = 0 high = 0 condition = And [
            Building name = "BLD_PARLAMENT"
            OwnedBy empire = Source.Owner
            ProducedByEmpire empire = Source.Owner
        ]

        // can't enqueue if already have an enqueued palace anywhere
        Number low = 0 high = 0 condition = And [
            Planet
            Enqueued
                type = Building
                name = "BLD_PARLAMENT"
                empire = Source.Owner
                low = 1
        ]
    ]
    effectsgroups = [
        [[SPECIES_LIKES_OR_DISLIKES_BUILDING_STABILITY_EFFECTS]]

        // flat bonus to influence of capital
        EffectsGroup
            scope = And [
                Object id = Source.PlanetID
                Planet
                OwnedBy empire = Source.ProducedByEmpireID
            ]
            effects = [
                SetEmpireCapital
                SetTargetInfluence value = Value
                     + 5 * (Statistic If condition = And [Source EmpireHasAdoptedPolicy empire = Source.Owner name = "PLC_DEMOCRACY"]) 
            ]

        // one economic policy slot
        EffectsGroup
            scope = Source
            effects = SetEmpireMeter empire = Source.Owner
                                     meter = "ECONOMIC_CATEGORY_NUM_POLICY_SLOTS" value = Value + 1 * (Statistic If condition = And [Source EmpireHasAdoptedPolicy empire = Source.Owner name = "PLC_DEMOCRACY"])
                                     
        // one social policy slot
        EffectsGroup
            scope = Source
            effects = SetEmpireMeter empire = Source.Owner
                                     meter = "SOCIAL_CATEGORY_NUM_POLICY_SLOTS" value = Value + 1

        // flat bonus to supply range of capital
        EffectsGroup
            scope = And [
                Object id = Source.PlanetID
                Planet
                OwnedBy empire = Source.ProducedByEmpireID
            ]
            stackinggroup = "PARLAMENT_SUPPLY_EFFECT"
            effects = SetMaxSupply value = Value
                        + 2 * (Statistic If condition = And [Source EmpireHasAdoptedPolicy empire = Source.Owner name = "PLC_DEMOCRACY"])

        // flat bonus to stability of capital
        EffectsGroup
            scope = And [
                Planet
                OwnedBy empire = Source.Owner
                Species name = Source.Planet.Species
            ]
            activation = And [
                OwnedBy empire = Source.ProducedByEmpireID
                ContainedBy And [
                    Object id = Source.PlanetID
                    Planet
                    Species
                    Capital
                ]
            ]
            stackinggroup = "PARLAMENT_HAPPIENESS"
            accountinglabel = "PARLAMENT_SPECIES_HAPPINESS"
            effects = SetTargetHappiness value = Value - 5 + 10 * (Statistic If condition = And [Source EmpireHasAdoptedPolicy empire = Source.Owner name = "PLC_DEMOCRACY"])
    ]
    icon = "icons/building/shield.png"

#include "/scripting/common/priorities.macros"
#include "/scripting/common/base_prod.macros"
#include "/scripting/common/misc.macros"
#include "/scripting/buildings/buildings.macros"
