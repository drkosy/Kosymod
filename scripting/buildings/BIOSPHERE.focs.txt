BuildingType
    name = "BLD_BIOSPHERE"
    description = "BLD_BIOSPHERE_DESC"
    buildcost = 90 * [[BUILDING_COST_MULTIPLIER]]
    buildtime = 3
    location = And [
        TargetPopulation low = 0.5
        Not Contains Building name = "BLD_BIOSPHERE"
        OwnedBy empire = Source.Owner
        Not Planet type = [Asteroids GasGiant]
    ]
    enqueuelocation = [[ENQUEUE_BUILD_ONE_PER_PLANET]]
    effectsgroups = [
        [[SPECIES_LIKES_OR_DISLIKES_BUILDING_STABILITY_EFFECTS]]
    
        EffectsGroup
            scope = And [
                Object id = Source.PlanetID
                Planet
            ]
            priority = [[TARGET_POPULATION_AFTER_SCALING_PRIORITY]]
            effects = [
            SetTargetPopulation value = Value 
                + (NamedReal name = "BLD_BIOSPHERE_POPULATON_FLAT" value = 2)
                + (NamedReal name = "BLD_BIOSPHERE_TECH_BONUS1" value = 2) * Statistic If condition = And [Source OwnerHasTech name = "CON_NDIM_STRC"]
                + (NamedReal name = "BLD_BIOSPHERE_TECH_BONUS2" value = 1) * Statistic If condition = And [Source OwnerHasTech name = "GRO_GAIA_TRANS"]
            SetTargetHappiness value = Value + (NamedReal name = "BLD_BIOSPHERE_HAPPY_BONUS" value = 2) * Statistic If condition = And [Source EmpireHasAdoptedPolicy empire = Target.Owner name = "PLC_POPULATION"]]
    ]
    icon = "icons/building/camp.png"

#include "/scripting/common/enqueue.macros"
#include "/scripting/common/priorities.macros"
#include "/scripting/common/base_prod.macros"
#include "/scripting/buildings/buildings.macros"
