Policy
    name = "PLC_CENTRALIZATION"
    description = "PLC_CENTRALIZATION_DESC"
    short_description = "PLC_CENTRALIZATION_SHORT_DESC"
    category = "ECONOMIC_CATEGORY"
    adoptioncost = floor(50 + 3.0 * [[POPULATION_OWNED_BY_EMPIRE]]^0.5)
    exclusions = [ "PLC_COLONIZATION" "PLC_INTERSTELLAR_INFRA" "PLC_FEUDALISM" "PLC_ISOLATION" ]
    unlock = [
        Item type = Building name = "BLD_REGIONAL_ADMIN"
    ]
    effectsgroups = [
        [[SPECIES_LIKES_OR_DISLIKES_POLICY_STABILITY_EFFECTS]]
        
    EffectsGroup
            scope = Source
            priority = [[TARGET_AFTER_SCALING_PRIORITY]]
            effects = SetEmpireMeter empire = Source.Owner meter = "ECONOMIC_CATEGORY_NUM_POLICY_SLOTS" value = Value + 2

        // Makes colony buildings and colony/outpost ship parts more expensive
        // See COLONIZATION_POLICY_MULTIPLIER macro in ../common/upkeep.macros

        // capital bonuses
        EffectsGroup
            scope = And [
                Capital
                OwnedBy empire = Source.Owner
                Happiness low = (NamedReal name = "PLC_CENTRALIZATION_MIN_STABILITY" value = 20)
            ]
            priority = [[TARGET_AFTER_SCALING_PRIORITY]]
            effects = [
                SetTargetIndustry value = Value + Target.Population
                    * (NamedReal name = "PLC_CENTRALIZATION_TARGET_INDUSTRY_PERPOP" value = 0.1 ) * Statistic If condition = And [Source EmpireHasAdoptedPolicy empire = Source.Owner name = "PLC_INDUSTRIALISM"]
                SetTargetResearch value = Value + Target.Population
                    * (NamedReal name = "PLC_CENTRALIZATION_TARGET_RESEARCH_PERPOP"
                                 value = 0.15 ) * Statistic If condition = And [Source EmpireHasAdoptedPolicy empire = Source.Owner name = "PLC_THINKTANKS"]
            ]

        EffectsGroup
            scope = And [
                Capital
                OwnedBy empire = Source.Owner
            ]
            effects = [
                SetMaxSupply value = Value
                    + (NamedReal name = "PLC_CENTRALIZATION_MAX_SUPPLY_FLAT" value = -1)
                SetMaxStockpile value = Value
                    + (NamedReal name = "PLC_CENTRALIZATION_MAX_STOCKPILE_FLAT" value = -2)
            ]

    ]
    graphic = "icons/policies/economic_centralization.png"

#include "/scripting/common/priorities.macros"
#include "/scripting/common/base_prod.macros"
#include "/scripting/policies/policies.macros"
