Policy
    name = "PLC_DEMOCRACY"
    description = "PLC_DEMOCRACY_DESC"
    short_description = "PLC_DEMOCRACY_SHORT_DESC"
    category = "SOCIAL_CATEGORY"
    adoptioncost = floor(2.0 * [[POPULATION_OWNED_BY_EMPIRE]]^0.5)
    exclusions = [ "PLC_RACIAL_PURITY" ]
  //  prerequisites = [ "PLC_LIBERTY" ]
    effectsgroups = [
        [[SPECIES_LIKES_OR_DISLIKES_POLICY_STABILITY_EFFECTS]]

        EffectsGroup
            scope = Source
            priority = [[TARGET_AFTER_SCALING_PRIORITY]]
            effects = SetEmpireMeter empire = Source.Owner meter = "ECONOMIC_CATEGORY_NUM_POLICY_SLOTS" value = Value + 1
        
        EffectsGroup
        scope = And [
            Planet
            OwnedBy empire = Source.Owner
            Species
        ]
        effects = [
            SetTargetHappiness value = Value - (NamedReal name = "PLC_DEMOCRACY_HAPPY_PENALTY_FLAT" value = 1)
        ]
        
        EffectsGroup
        scope = And [
            Planet
            OwnedBy empire = Source.Owner
            Species
            Focus type = "FOCUS_INFLUENCE"
            Happiness low = (NamedReal name = "PLC_DEMOCRACY_MIN_STABILITY" value = 8)
        ]
            effects = SetTargetInfluence value = Value + (NamedReal name = "PLC_DEMOCRACY_TARGET_INFLUENCE_FLAT"
                                     value = 1)
        EffectsGroup
            scope = And [
                OwnedBy empire = Source.Owner
                TargetPopulation low = 0.0001
            ]
            priority = [[TARGET_OVERRIDE_PRIORITY]]
            effects = SetTargetInfluence value = Value - 
                (NamedReal name = "DEMOCRACY_ADMIN_COSTS_PER_PLANET" value = 0.1) * min(5, ((
                    Statistic Count condition = And [
                        Planet
                        OwnedBy empire = Source.Owner
                        Species
                        Not Species name = "SP_EXOBOT"
                    ] +
                    ((NamedReal name = "DEMOCRACY_ADMIN_COSTS_PER_OUTPOST" value = 0.05) *
                        (Statistic Count condition = And [
                            Planet
                            OwnedBy empire = Source.Owner
                            Or [
                                Not Species
                                Species name = "SP_EXOBOT"
                            ]
                        ])
                    )
                )^0.5))
    ]
    graphic = "icons/policies/social_democracy.png"

#include "/scripting/common/base_prod.macros"
#include "/scripting/common/priorities.macros"
#include "/scripting/policies/policies.macros"
