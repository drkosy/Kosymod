Policy
    name = "PLC_THINKTANKS"
    description = "PLC_THINKTANKS_DESC"
    short_description = "PLC_THINKTANKS_SHORT_DESC"
    category = "ECONOMIC_CATEGORY"
    adoptioncost = floor(2.0 * [[POPULATION_OWNED_BY_EMPIRE]]^0.5)
    exclusions = [ "PLC_TECHNOCRACY" "PLC_INDUSTRIALISM" "PLC_METROPOLES"]
    effectsgroups = [
        [[SPECIES_LIKES_OR_DISLIKES_POLICY_STABILITY_EFFECTS]]
    
        EffectsGroup
            scope = And [
                Planet
                OwnedBy empire = Source.Owner
                Not Planet type = Asteroids
            ]
            priority = [[TARGET_AFTER_SCALING_PRIORITY]]
            effects = [
                SetTargetIndustry value = Value
                    + (NamedReal name = "PLC_THINKTANKS_TARGET_INDUSTRY_FLAT" value = -2)
                SetTargetConstruction value = Value
                    + (NamedReal name = "PLC_THINKTANKS_TARGET_CONSTRUCTION_FLAT" value = -10)
            ]
        
        EffectsGroup
            scope = And [
                OwnedBy empire = Source.Owner
                Focus type = "FOCUS_RESEARCH"
                Happiness low = (NamedReal name = "PLC_THINKTANKS_RESEARCH_STABILITY" value = 8)
            ]
            priority = [[TARGET_AFTER_SCALING_PRIORITY]]
            effects = [
                SetTargetResearch value = Value + (NamedReal name = "PLC_THINKTANKS_RESEARCH_BONUS" value = 1)
                SetTargetResearch value = Value + min(4, 0.05 * Target.Population * Statistic If condition = And [Source EmpireHasAdoptedPolicy empire = Source.Owner name = "PLC_CENTRALIZATION"])
                SetTargetResearch value = Value + 3 * Statistic If condition = And [Source EmpireHasAdoptedPolicy empire = Source.Owner name = "PLC_INTERSTELLAR_INFRA"]
                SetTargetHappiness value = Value + (NamedReal name = "PLC_THINKTANKS_HAPPINESS_BONUS" value = 2)
            ]
            
        EffectsGroup
            scope = And [
                ProductionCenter
                OwnedBy empire = Source.Owner
                Focus type = "FOCUS_INFLUENCE"
                Happiness low = (NamedReal name = "PLC_THINKTANKS_INFLUENCE_STABILITY" value = 5)
            ]
            priority = [[TARGET_AFTER_SCALING_PRIORITY]]
            effects = [
                SetTargetInfluence value = Value + (NamedReal name = "PLC_THINKTANKS_INFLUENCE_BONUS" value = 1)
                SetTargetInfluence value = Value + min(5, 0.1 * Target.Population * Statistic If condition = And [Source EmpireHasAdoptedPolicy empire = Source.Owner name = "PLC_CENTRALIZATION"])
                SetTargetInfluence value = Value + 3 * Statistic If condition = And [Source EmpireHasAdoptedPolicy empire = Source.Owner name = "PLC_INTERSTELLAR_INFRA"]
            ]
        
    ]
    graphic = "icons/policies/economic_thinktanks.png"

#include "/scripting/common/base_prod.macros"
#include "/scripting/common/priorities.macros"
#include "/scripting/policies/policies.macros"
